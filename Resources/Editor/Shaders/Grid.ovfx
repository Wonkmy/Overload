#shader vertex
#version 450 core

layout (location = 0) in vec3 geo_Pos;
layout (location = 1) in vec2 geo_TexCoords;
layout (location = 2) in vec3 geo_Normal;

layout (std140) uniform EngineUBO
{
    mat4 ubo_Model;
    mat4 ubo_View;
    mat4 ubo_Projection;
    vec3 ubo_ViewPos;
    float ubo_Time;
};

out VS_OUT
{
    vec3 FragPos;
    vec2 TexCoords;
} vs_out;

void main()
{
    vs_out.FragPos = vec3(ubo_Model * vec4(geo_Pos, 1.0));
    vs_out.TexCoords = vs_out.FragPos.xz;
    gl_Position = ubo_Projection * ubo_View * vec4(vs_out.FragPos, 1.0);
}

#shader fragment
#version 450 core

out vec4 FRAGMENT_COLOR;

layout (std140) uniform EngineUBO
{
    mat4 ubo_Model;
    mat4 ubo_View;
    mat4 ubo_Projection;
    vec3 ubo_ViewPos;
    float ubo_Time;
};

in VS_OUT
{
    vec3 FragPos;
    vec2 TexCoords;
} fs_in;

uniform vec3 u_Color;

float MAG(float lineWidth, float p_lp)
{
    const vec2 coord = fs_in.TexCoords / p_lp;
    const vec2 grid = abs(fract(coord - 0.5) - 0.5) / fwidth(coord);
    const float line = min(grid.x, grid.y);
    const float lineResult = lineWidth - min(line, lineWidth);
    return lineResult;
}

float Grid(float lineWidth, float height, float a, float b, float c)
{
    const float cl = MAG(lineWidth, a);
    const float ml = MAG(lineWidth, b);
    const float fl = MAG(lineWidth, c);
    const float cmit = 10.0f;
    const float cmet = 40.0f;
    const float mfit = 80.0f;
    const float mfet = 160.0f;
    const float df = clamp((height - cmit) / (cmet - cmit), 0.0f, 1.0f);
    const float dff = clamp((height - mfit) / (mfet - mfit), 0.0f, 1.0f);
    const float inl = mix(cl, ml, df);
    const float fnl = mix(inl, fl, dff);
    return fnl;
}

void main()
{
    const float height = distance(ubo_ViewPos.y, fs_in.FragPos.y);
    const float gridA = Grid(0.5f, height, 1.0f, 4.0f, 8.0f);
    const float gridB = Grid(0.7f, height, 4.0f, 16.0f, 32.0f);
    const float grid = gridA * 0.7f + gridB * 0.7f;

    // Calculate view direction vector (from camera to fragment)
    const vec3 viewDir = normalize(fs_in.FragPos - ubo_ViewPos);

    // Calculate how parallel the view direction is to the horizon
    // When looking at horizon, viewDir.y will be close to 0
    const float horizonFactor = abs(viewDir.y);

    // Define how much we want to fade when looking at horizon
    const float horizonMinFade = 0.02f; // Minimum fade at horizon
    const float horizonFadeStart = 0.0f; // Start fading at this angle
    const float horizonFadeEnd = 1.0f; // Full grid strength at this angle

    // Calculate horizon fade factor (1.0 = full grid, 0.0 = fully faded)
    const float horizonFade =
        horizonMinFade
        + (1.0f - horizonMinFade)
        * smoothstep(horizonFadeStart, horizonFadeEnd, horizonFactor);

    FRAGMENT_COLOR = vec4(u_Color, grid * horizonFade);
}
